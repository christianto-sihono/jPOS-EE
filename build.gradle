apply from: 'libraries.gradle'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.owasp:dependency-check-gradle:5.2.1'
    }
}


allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'maven'
    apply plugin: 'org.owasp.dependencycheck'

    group = 'org.jpos.ee'
    version = '2.2.7-SNAPSHOT'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'signing'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    [ compileJava, compileTestJava, javadoc ]*.options*.encoding = 'UTF-8'
    def isSnapshot = version.contains("SNAPSHOT")
    def mavenCentralRepo = isSnapshot ?
        'https://oss.sonatype.org/content/repositories/snapshots/' :
        'https://oss.sonatype.org/service/local/staging/deploy/maven2';

    project.archivesBaseName="jposee-${project.name}"
    javadoc.failOnError = false

    def pomConfig = {
        name 'jPOS-EE Project'
        description """
    jPOS Extended Edition
  """
        url 'http://jpos.org'
        inceptionYear '1998'
        organization {
            name 'jPOS.org'
            url 'http://jpos.org'
        }
        issueManagement {
            system 'YouTrack'
            url 'https://jpos.org/issues'
        }
        scm {
            url "https://github.com/jpos/jPOS"
            connection "scm:git:https://github.com/jpos/jPOS-EE.git"
            developerConnection "scm:git:git@github.com:jpos/jPOS-EE.git"
        }
        licenses {
            license {
                name 'GNU AFFERO GENERAL PUBLIC LICENSE'
                url 'http://www.gnu.org/licenses/agpl-v3.html'
                comments 'See http://jpos.org/license for more details.'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'jpos-team'
                name 'jPOS Development Team'
                email 'info@jpos.org'
            }
        }
    }

    configure(install.repositories.mavenInstaller) {
        pom.project pomConfig
    }
    signing {
        required { !isSnapshot }
        sign configurations.archives
    }
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project pomConfig
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                repository(url: 'file:///opt/local/maven')
                /*
                repository(url: mavenCentralRepo) {
                    authentication(userName: mavenCentralUsername, password: mavenCentralPassword)
                }
                */
            }
        }
    }
    test {
        useJUnitPlatform()
        maxParallelForks = Runtime.runtime.availableProcessors()
    }
    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        archiveClassifier.set('sources')
    }

    artifacts {
        archives sourceJar
    }

    repositories {
        mavenCentral()
        maven { url 'https://jpos.org/maven' }
        maven { url 'https://download.oracle.com/maven' }
        maven { url 'https://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
        jcenter()
        mavenLocal()
    }

    dependencies {
        testImplementation libraries.junit
        //Java 10 support
        testImplementation('com.sun.xml.bind:jaxb-impl:2.1.2')
        testImplementation('com.sun.activation:javax.activation:1.2.0')
        testImplementation('javax.xml.bind:jaxb-api:2.3.0')
    }

    task allDeps(type: DependencyReportTask) {}
}

task allSources(type: Jar,dependsOn: subprojects.assemble) {
    archiveBaseName = 'jposee-sources'
    from subprojects.collect { project ->
        project.sourceSets.main.allSource
    }
}

// Configure IDEA to use Git
idea.project.ipr {
  withXml { provider ->
    provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping.@vcs = 'Git'
  }
}

